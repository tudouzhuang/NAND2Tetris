// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    Mux16(a=x,b=false,sel=zx,out=zerox);
    Not16(in=zerox,out=notx);
    Mux16(a=zerox,b=notx,sel=nx,out=negatex);
    Mux16(a=y,b=false,sel=zy,out=zeroy);
    Not16(in=zeroy,out=noty);
    Mux16(a=zeroy,b=noty,sel=ny,out=negatey);
    Add16(a=negatex,b=negatey,out=xaddy);
    And16(a=negatex,b=negatey,out=xandy);
    Mux16(a=xandy,b=xaddy,sel=f,out=firstout);
    Not16(in=firstout,out=notout);
    Mux16(a=firstout,b=notout,sel=no,out[0..7]=aout,out[8..15]=bout);
    Or8Way(in=aout,out=or1);
    Or8Way(in=bout,out=or2);
    Or(a=or1,b=or2,out=orout);
    Not(in=orout,out=zr);
    And16(a[0..7]=aout,a[8..15]=bout,b[15]=true,b[0..14]=false,out[8..15]=andout);
    Or8Way(in=andout,out=ng);
    Mux16(a=firstout,b=notout,sel=no,out=out);
    }


